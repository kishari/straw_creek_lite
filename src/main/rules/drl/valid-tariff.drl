package MTPLTariffRules

import hu.dbx.screek.model.facts.*;
import hu.dbx.screek.model.*;
import org.joda.time.DateTime;
import java.util.Date;

function boolean isNullString(String s) {
		return ("".equals(s) || s == null);
}

function boolean isEq(String str1, String str2) {
	if (str1 != null && str2 != null) {
		return str1.equals(str2);
	}
	return false;
}

function int getActualYear() {
	DateTime now = new DateTime(new Date());
	return now.getYear();
}

function String getFormattedDate(Date d) {
		DateTime d1 = new DateTime(d);
		String date = String.valueOf(d1.getYear());
		if (d1.getMonthOfYear() < 10) {
			date = date + "-0" + String.valueOf(d1.getMonthOfYear() );
		}
		else {
			date = date + "-" + String.valueOf(d1.getMonthOfYear() );
		}
		if (d1.getDayOfMonth() < 10) {
			date = date + "-0" + String.valueOf(d1.getDayOfMonth() );
		}
		else {
			date = date + "-" + String.valueOf(d1.getDayOfMonth() );
		}
				
		return date;
}

function boolean isNullInt(Integer i) {
		return (i == null || i == 0);
}

function boolean firstDateIsAfterOrEqualsThanSecondDate(DateTime q, DateTime v) {
	return ( q.isAfter(v) || q.isEqual(v) );
}

function boolean firstDateIsAfter(Date s0, Date e0) {
	if (s0 == null || e0 == null) {
		return false;
	}
	
	DateTime s = new DateTime(s0);
	DateTime e = new DateTime(e0);	
	
	DateTime temp = s.withDate(s.getYear(), s.getMonthOfYear(), s.getDayOfMonth());
	
	return temp.isAfter(e);    
}

function boolean isBetween(Integer lowerBound, Integer upperBound, Integer value) {
	if ( value == null ) {
		return false;
	} 
	else {
		return (lowerBound <= value && upperBound >= value);
	}
}

function int yearsBetween(Date s, Date e) {
	return Math.abs(new DateTime(s).getYear() - new DateTime(e).getYear());  
}

/*****************************************************************************
				 Biztosítás szintű validációk 
*****************************************************************************/

rule "validate bonusMalus (isNull)"
    agenda-group "valid-tariff"
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN,
					eval( isNullString(bonusMalus) ) )
		$v : Vehicle()
		$c : Context()
		$vt: VehicleTypeDef( typeCode == $v.typeCode, bonusMalus == true, version == $c.version )
	then
		$q.setBonusMalus(null);
		insert( Message.create("29") );
end

rule "validate bonusMalus (not valid)"
    agenda-group "valid-tariff"

	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN,
					eval( !isNullString(bonusMalus) ) )
		$c : Context()
		not(exists BonusMalusModFactorDef( bonusMalus == $q.bonusMalus, version == $c.version ) )	
	then
		insert( Message.create("30", $q.getBonusMalus() ) );
end

rule "validate startDate (isNull)"
    agenda-group "valid-tariff"
      
	when
		$q : Quote( startDate == null )
		$c : Context()				
	then
		insert( Message.create("24") );		
end

rule "validate insurance duration (isNull)"
    agenda-group "valid-tariff"
      
	when
		$q : Quote( eval( isNullInt(durationType) ) )
		$c : Context()
	then
		$q.setDurationType(null);
		insert( Message.create("12") );		
end

rule "validate insurance duration (not valid)"
    agenda-group "valid-tariff"
      
	when
		$q : Quote( eval( !isNullInt(durationType) ) )
		$c : Context()
		not(exists DurationTypeDef( durationType == $q.durationType, version == $c.version ) )
		
	then
		insert( Message.create("11", $q.getDurationType().toString() ) );		
end

rule "validate payment method (isNull)"
    agenda-group "valid-tariff"
      
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN &&  
					eval( isNullInt(paymentMethod) ) )
		$c : Context()
	then
		$q.setPaymentMethod(null);
		insert( Message.create("6") );		
end

rule "validate payment method (not valid)"
    agenda-group "valid-tariff"
   
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN &&  
					eval( !isNullInt(paymentMethod) ) )
		$c : Context()					
		not(exists PaymentMethodDef( paymentMethod == $q.paymentMethod, version == $c.version ) )
			
	then
		insert( Message.create("5", $q.getPaymentMethod().toString() ) );		
end

rule "validate payment frequency (isNull)"
    agenda-group "valid-tariff"
      
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN && 
					eval( isNullInt(paymentFrequency) ) )
		$c : Context()					
	then
		$q.setPaymentFrequency(null);
		insert( Message.create("8") );		
end

rule "validate payment frequency (not valid)"
    agenda-group "valid-tariff"
      
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN &&
					eval( !isNullInt(paymentFrequency) ) )
		$c : Context()
		not(exists PaymentFreqDef( frequencyCode == $q.paymentFrequency, version == $c.version ) )
			
	then
		insert( Message.create("7", $q.getPaymentFrequency().toString() ) );
end

rule "validate payment frequency (not valid - havi eseten nem csoportos)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$p : PaymentMethodDef( paymentMethod == Constants.PM_BANKI_CSOPORTOS_BESZEDES, version == $c.version )
		$q : Quote( durationType == Constants.DT_HATAROZATLAN && 
					paymentMethod != $p.paymentMethod &&
					paymentFrequency == Constants.PF_HAVI )				
			
	then
		insert( Message.create("27") );
end

rule "validate payment frequency (not valid - bizonyos jármuveknel egy osszegben kell fizetni)"
    agenda-group "valid-tariff"
       
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN, 
					paymentFrequency != Constants.PF_EVES )
		$v : Vehicle( typeCode == Constants.TC_KONNYU_POTKOCSI ||
					  typeCode == Constants.TC_NEHEZ_POTKOCSI ||
					  typeCode == Constants.TC_SZEMELYGEPKOCSI_UTANFUTO ||
					  typeCode == Constants.TC_LAKOKOCSI ||
					  typeCode == Constants.TC_MOTORKEREKPAR_UTANFUTO ||
					  typeCode == Constants.TC_SEGEDMOTOROS_KEREKPAR )
		$vt: VehicleTypeDef( typeCode == $v.typeCode, version == $c.version )
	then
		insert( Message.create("28", $v.getTypeCode().toString(), $vt.getDescription() ) );
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (egyeb jarmu)"
	agenda-group "valid-tariff"
	
	salience -5 
	when
		$c : Context()
		$q : Quote()
		$v : Vehicle()
		$b : BaseTariffDef( durationType == $q.durationType,
		    		        vehicleType == $v.typeCode, 
		    		        version == $c.version )
		$vt: VehicleTypeDef( typeCode == $v.typeCode, version == $c.version )		    		   
		eval ( !$b.isValid( $q.getStartDate() ) )
		eval ( ($q.getDurationType() == Constants.DT_HATAROZATLAN && 
			   	$v.getTypeCode() != Constants.TC_TEHERGEPJARMU && 
		   	   	$v.getTypeCode() != Constants.TC_MOTORKEREKPAR && 
		   	   	$v.getTypeCode() != Constants.TC_AUTOBUSZ) ||
		   	   	($q.getDurationType() == Constants.DT_HATAROZOTT) ) 
	    not (exists (Message( code == "24") ) )	
	then	
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
		
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (motorkerekpar)"
	agenda-group "valid-tariff"
	
	salience -5 
	when
		$c : Context()
		$q : Quote()
		$v : Vehicle( typeCode == Constants.TC_MOTORKEREKPAR )
		$vt: VehicleTypeDef( typeCode == $v.typeCode, version == $c.version )	
		$b : MotorBikeBaseTariffDef( durationType == $q.durationType, version == $c.version ) 
		eval ( isBetween($b.getCubicCapacityMin(), $b.getCubicCapacityMax(), $v.getCubicCapacity() ) )
		eval ( !$b.isValid( $q.getStartDate() ) )
		not (exists (Message( code == "24") ) )
						
	then
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (autobusz)"
	agenda-group "valid-tariff"
	
	salience -5 
	when
	 	$c : Context()
		$q : Quote()
		$v : Vehicle( typeCode == Constants.TC_AUTOBUSZ )
		$vt: VehicleTypeDef( typeCode == $v.typeCode, version == $c.version )			
		$b : AutoBusBaseTariffDef( durationType == $q.durationType, version == $c.version )
		eval ( isBetween($b.getSeatingCapacityMin(), $b.getSeatingCapacityMax(), $v.getSeatingCapacity() ) )
		eval ( !$b.isValid( $q.getStartDate() ) )
		not (exists (Message( code == "24") ) )
						
	then
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (tehergepjarmu)"
	agenda-group "valid-tariff"
	
	salience -5 
	when
		$ctx : Context()
		$c : ConstantsDef( version == $ctx.version, eval(isEq(name, "LEGAL_PERSON") ) )
		$q : Quote()
		$p : Partner()
		$v : Vehicle( typeCode == Constants.TC_TEHERGEPJARMU )
		$vt: VehicleTypeDef( typeCode == $v.typeCode, version == $ctx.version )
		$b : TruckBaseTariffDef( durationType == $q.durationType,
								 eval( isEq(genderCode, $p.getGenderCode() ) ),
								 version == $ctx.version )
		eval ( isBetween( $b.getCarryingCapacityMin(), 
						  $b.getCarryingCapacityMax(), 
						  $v.getCarryingCapacity() ) )
		eval ( ( !isEq( $p.getGenderCode(), $c.getValue() ) &&
				 isBetween( $b.getPartnerAgeMin(), 
				 			$b.getPartnerAgeMax(), 
				 			yearsBetween( $p.getDateOfBirth(), $q.getStartDate() ) ) ) || 
			   ( isEq( $p.getGenderCode(), $c.getValue() ) ) )
		eval ( !$b.isValid( $q.getStartDate() ) )
		not (exists (Message( code == "24") ) )				
	then
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
end

/*****************************************************************************
				        Jármű szintű validációk 
*****************************************************************************/
rule "validate yearOfProduction (isNull)"
    agenda-group "valid-tariff"      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU &&
					  eval( isNullInt( yearOfProduction ) ) )
				
	then
		insert( Message.create("36") );		
end

rule "validate yearOfProduction (not valid)"
    agenda-group "valid-tariff"      
	when
	 	$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU && eval ( !isNullInt( yearOfProduction ) ) )					  
		eval( $v.getYearOfProduction() < 1900 || $v.getYearOfProduction() > getActualYear() + 1)
				
	then
		insert( Message.create("37", $v.getYearOfProduction().toString() ) );		
end

rule "validate vehicle typecode (isNull)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$v : Vehicle( eval( isNullInt(typeCode) ) )
		
	then
		$v.setTypeCode(null);
		insert( Message.create("4") );		
end

rule "validate vehicle typecode (not valid)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$v : Vehicle( eval( !isNullInt(typeCode) ) )
		not(exists VehicleTypeDef( typeCode == $v.typeCode, version == $c.version) )
		
	then
		insert( Message.create("3", $v.getTypeCode().toString() ) );		
end

rule "validate vehicle typecode (not valid - proba rendszam nincs hatarozatlan esetben)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_PROBA_RENDSZAM )
		$vt: VehicleTypeDef( typeCode == $v.typeCode, version == $c.version )
	then
		insert( Message.create("31", $v.getTypeCode().toString(), $vt.getDescription() ) );		
end

rule "validate vehicle typecode (not valid - segedmotoros kerekpar, trolibusz nincs hatarozott esetben)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZOTT )
		$v : Vehicle( typeCode == Constants.TC_SEGEDMOTOROS_KEREKPAR ||
					  typeCode == Constants.TC_TROLIBUSZ )
		$vt: VehicleTypeDef( typeCode == $v.typeCode, version == $c.version )
	then
		insert( Message.create("32", $v.getTypeCode().toString(), $vt.getDescription() ) );		
end

rule "validate vehicle make (not valid)"
    agenda-group "valid-tariff"
       
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU && 
					  eval ( !isNullString(make) ) )
		not(exists MakeDef( make == $v.make, version == $c.version ) )
		
	then
		insert( Message.create("9", $v.getMake() ) );		
end

rule "validate vehicle make (isNull)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU && 
					  eval( isNullString(make) ))
		
	then
		insert( Message.create("10") );		
end

rule "validate operationalModality (not valid)"
    agenda-group "valid-tariff"
     
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( eval( !isNullInt(operationalModality) ) )
		not(exists OperationalModalityDef( operationalModality == $v.operationalModality, version == $c.version ) )
			
	then
		insert( Message.create("16", $v.getOperationalModality().toString() ) );
end

rule "validate operationalModality (isNull)"
    agenda-group "valid-tariff"
      
	when	
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( eval( isNullInt(operationalModality) ) )
			
	then
		$v.setOperationalModality(null);
		insert( Message.create("17") );
end

rule "validate power (isNull)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU && 
					  eval( isNullInt(maximumNettoPower) ) )
			
	then
		$v.setMaximumNettoPower(null);
		insert( Message.create("18") );
end

rule "validate cubicCapacity (isNull)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( (typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_MOTORKEREKPAR) &&  
					  eval( isNullInt(cubicCapacity) ) )
			
	then
		$v.setCubicCapacity(null);
		insert( Message.create("19") );
end

rule "validate seatingCapacity (isNull)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_AUTOBUSZ && 
					  eval( isNullInt(seatingCapacity) ) )
			
	then
		$v.setSeatingCapacity(null);
		insert( Message.create("20") );
end

rule "validate carryingCapacity (isNull)"
    agenda-group "valid-tariff"
    
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_TEHERGEPJARMU && 
					  eval( isNullInt(carryingCapacity) ) )
			
	then
		$v.setCarryingCapacity(null);
		insert( Message.create("21") );
end

/*****************************************************************************
				        Partner szintű validációk 
*****************************************************************************/
rule "validate dateOfBirth (isNull)"
    agenda-group "valid-tariff"
      
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_TEHERGEPJARMU )
		$p : Partner( dateOfBirth == null &&
					  eval( !isEq(genderCode, "3") ) )
				
	then
		insert( Message.create("23") );		
end

rule "validate postcode (isNull)"
    agenda-group "valid-tariff"    
    
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU )
		$p : Partner( eval( isNullInt(postCode) ) )
				
	then
		$p.setPostCode(null);
		insert( Message.create("2") );		
end

rule "validate postcode (not valid)"
    agenda-group "valid-tariff"  
        
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU )
		$p : Partner( eval( !isNullInt(postCode) ) )
		not(exists AreaDef( postCode == $p.postCode, version == $c.version ) )
		
	then
		insert( Message.create("1", $p.getPostCode().toString() ) );		
end

rule "validate genderCode (isNull)"
    agenda-group "valid-tariff"
     
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_TEHERGEPJARMU )
		$p : Partner( eval( isNullString( genderCode ) ) )
	then
		insert( Message.create("14") );		
end

rule "validate genderCode (not valid)"
    agenda-group "valid-tariff"
          
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_TEHERGEPJARMU )
		$p : Partner( eval( !isNullString( genderCode ) ) )
		not(exists GenderDef( eval( isEq(genderCode, $p.getGenderCode() ) ), version == $c.version ) )
			
	then
		insert( Message.create("13", $p.getGenderCode() ) );
end

rule "validate yearOfDrivingLicense (isNull)"
    agenda-group "valid-tariff"
    
	when
		$c : Context()
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU )
		$p : Partner( eval( isNullInt(yearOfDrivingLicense) ) )
			
	then
		$p.setYearOfDrivingLicense(null);
		insert( Message.create("22") );
end



/*****************************************************************************
				        		Egyéb
*****************************************************************************/
rule "validate szemelygepjarmu discounts (startDate >= 2010 jan 1 and insuranceDuration = HATAROZATLAN)"
    agenda-group "valid-tariff"
    salience -50
    no-loop
	when
		$c : Context()
		$q : Quote( takeOutWithCasco == true ||
					childPreference == true ||
					mkbPartner == true ||
					online == true ||
					emailGranted == true ||
					extraDamageExemption == true ||
					groupedCollection == true )
		$v : Vehicle()
		$d : DurationTypeDef( durationType == Constants.DT_HATAROZATLAN, version == $c.version )
		$vt: VehicleTypeDef( typeCode == Constants.TC_SZEMELYGEPJARMU, version == $c.version )
		not (exists ( Message() ) )  
		eval ( !firstDateIsAfterOrEqualsThanSecondDate(new DateTime($q.getStartDate()), new DateTime(2010, 1, 1, 0, 0, 0, 0) ) 
			  || ($d.getDurationType() != $q.getDurationType() ) 
			  || ($vt.getTypeCode() != $v.getTypeCode() ) )
		
	then
		insert( Message.create("15", "2009-12-31" ) );
		modify ( $q ) {	
			setTakeOutWithCasco(false),
			setChildPreference(false),
			setMkbPartner(false),
			setOnline(false),
			setEmailGranted(false),
			setExtraDamageExemption(false),
			setGroupedCollection(false)	
		}
end