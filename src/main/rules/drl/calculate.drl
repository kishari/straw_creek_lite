package MTPLTariffRules

#list any import classes here.
import hu.dbx.screek.model.facts.*;
import hu.dbx.screek.model.*;
import java.util.Date;
import org.joda.time.DateTime;
import java.util.ArrayList;

function int yearsBetween(Date s, int startYear) {
	DateTime s1 = new DateTime(s);
	return (startYear - s1.getYear());  
}

rule "szemelygepjarmu alaptarifa beallitas"
	agenda-group "calculate"
	when
		$q : Quote()
		$v : Vehicle( typeCode != "MOTORKEREKPAR" &&
					  typeCode != "AUTOBUSZ")
		$b : BaseTariffDef( vehicleType == $v.typeCode )
						
	then
		$q.getResult().setBaseTariff( $b.getValue() );
end

rule "motorkerekpar alaptarifa beallitas"
	agenda-group "calculate"
	when
		$q : Quote()
		$v : Vehicle( typeCode == "MOTORKEREKPAR")
		$m : MotorBikeCCModFactorDef( capacityMin <= $v.cubicCapacity &&
									  capacityMax >= $v.cubicCapacity)
		$b : BaseTariffDef( vehicleType == $v.typeCode )
						
	then
		$q.getResult().setBaseTariff( (int)Math.round($b.getValue() * $m.getValue() ) );
end

rule "autobusz alaptarifa beallitas"
	agenda-group "calculate"
	when
		$q : Quote()
		$v : Vehicle( typeCode == "AUTOBUSZ")
		$m : AutoBusModFactorDef( seatingCapacityMin <= $v.seatingCapacity &&
								  seatingCapacityMax >= $v.seatingCapacity)
		$b : BaseTariffDef( vehicleType == $v.typeCode )
						
	then
		$q.getResult().setBaseTariff( (int)Math.round($b.getValue() * $m.getValue() ) );
end

rule "area and partner age modfactor"
	agenda-group "calculate"
	when
		$q : Quote()
		$p : Partner()
		$a : AreaDef( postCode == $p.postCode )
		$mf: PartnerAgeModFactorDef( tariffCode == $a.tariffCode )
		eval ( $a.isValid( $q.getStartDate() ) &&
		 	   $mf.isValid( $q.getStartDate() ) &&
		 	   $mf.getAgeMin() <= yearsBetween($p.getDateOfBirth(), $q.getStartYear()) &&
		 	   $mf.getAgeMax() >= yearsBetween($p.getDateOfBirth(), $q.getStartYear()) )
				
	then
		insert(new ModFactor("AREA-AGE-MULTIPLIER", $mf.getValue(), $p));
end

rule "Egyeni szorzok"
	agenda-group "calculate"
	salience -30
	
    when
        $q : Quote()
        $p : Partner( quote == $q )
        $modfactors : ArrayList()
                   		from collect( ModFactor( partner == $p ) )

    then
    	for (Object mf : $modfactors){
    		$p.addModFactor((ModFactor) mf);
    	}
end


