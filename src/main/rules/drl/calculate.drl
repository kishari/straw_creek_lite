package MTPLTariffRules

import hu.dbx.screek.model.facts.*;
import hu.dbx.screek.model.*;
import java.util.Date;
import org.joda.time.DateTime;
import org.joda.time.Days;
import java.util.ArrayList;
import java.math.BigDecimal;

function boolean isNullInt(Integer i) {
		return (i == null || i == 0);
}

function boolean isEq(String str1, String str2) {
		if (str1 != null && str2 != null) {
			return str1.equals(str2);
		}
		return false;		
}

function double round(double val, int precision) {
	BigDecimal temp = new BigDecimal(val).setScale(precision, BigDecimal.ROUND_HALF_UP);
	
	return temp.doubleValue();
}

function boolean isBetween(Integer lowerBound, Integer upperBound, Integer value) {
	if ( value == null ) {
		return false;
	}
		if ( value == -1 ) {
		value = 0;
	}
 		return (lowerBound <= value && upperBound >= value);
}

function int yearsBetween(Date s, Date e) {
	return Math.abs(new DateTime(s).getYear() - new DateTime(e).getYear());  
}

function int daysBetween(Date s, Date e) {
	return Days.daysBetween(new DateTime(s), new DateTime(e)).getDays() + 1;
}

function int getYear(Date d) {
	return new DateTime(d).getYear();
}

function boolean firstIsAfter(DateTime s, DateTime e) {
	DateTime temp = s.withDate(s.getYear(), s.getMonthOfYear(), s.getDayOfMonth());
	
	return temp.isAfter(e);    
}

/*****************************************************************************
							  Alaptarifa beállítás 
*****************************************************************************/
rule "temp baseTariff parameter beszuras (Ez fogja tarolni szgk-nal a make_power X alaptarifa erteket)"
	agenda-group "calculate"
	salience 50
	when
		$q : Quote()
	then
		ParamDef p = new ParamDef();
		p.setKey("BASETARIFF-TEMP");
		p.setValue("UNKNOWN");	
		insert( p );
		
end

rule "alaptarifa beallitas"
	agenda-group "calculate"
	salience 30
	when
		$q : Quote()
		$v : Vehicle()
		$b : BaseTariffDef( durationType == $q.durationType,
							vehicleType == $v.typeCode )
		eval ( $b.isValid( $q.getStartDate() ) )
		eval ( ($q.getDurationType() == Constants.DT_HATAROZATLAN && 
			    !Constants.TC_TEHERGEPJARMU.equals($v.getTypeCode()) && 
		   	    !Constants.TC_MOTORKEREKPAR.equals($v.getTypeCode()) && 
		   	    !Constants.TC_AUTOBUSZ.equals($v.getTypeCode()) ) ||
		   	   	($q.getDurationType() == Constants.DT_HATAROZOTT) ) 
	then		
		$q.getResult().setBaseTariff( $b.getValue() );
		
end

rule "alaptarifa beallitas (motorkerekpar)"
	agenda-group "calculate"
	salience 30
	when
		$q : Quote()
		$v : Vehicle( typeCode == Constants.TC_MOTORKEREKPAR )
		$b : MotorBikeBaseTariffDef( durationType == $q.durationType ) 
		eval ( isBetween($b.getPowerMin(), $b.getPowerMax(), $v.getMaximumNettoPower() ) )
		eval ( $b.isValid( $q.getStartDate() ) )
						
	then
		$q.getResult().setBaseTariff( $b.getValue() );
end

rule "alaptarifa beallitas (autobusz)"
	agenda-group "calculate"
	salience 30	
	when
		$q : Quote()
		$v : Vehicle( typeCode == Constants.TC_AUTOBUSZ )
		$b : AutoBusBaseTariffDef( durationType == $q.durationType )
		eval ( isBetween($b.getSeatingCapacityMin(), $b.getSeatingCapacityMax(), $v.getSeatingCapacity() ) )
		eval ( $b.isValid( $q.getStartDate() ) )
						
	then
		$q.getResult().setBaseTariff( $b.getValue() );
end

rule "alaptarifa beallitas (tehergepjarmu)"
	agenda-group "calculate"
	salience 30	
	when
		$q : Quote()
		$p : Partner()
		$v : Vehicle( typeCode == Constants.TC_TEHERGEPJARMU )
		$b : TruckBaseTariffDef( durationType == $q.durationType,
								 genderCode == $p.genderCode )
		eval ( isBetween( $b.getAllowedWeightMin(), 
						  $b.getAllowedWeightMax(), 
						  $v.getMaximumAllowedWeight() ) )
		eval ( ( $p.getGenderCode() != Constants.JOGI_SZEMELY &&
				 isBetween( $b.getPartnerAgeMin(), 
				 			$b.getPartnerAgeMax(), 
				 			yearsBetween( $p.getDateOfBirth(), $q.getStartDate() ) ) ) || 
			   ( $p.getGenderCode() == Constants.JOGI_SZEMELY ) )
		eval ( $b.isValid( $q.getStartDate() ) )
						
	then
		$q.getResult().setBaseTariff( $b.getValue() );
end

/*****************************************************************************
				 Díjszámítási paraméterek beállítása 
*****************************************************************************/
rule "alaptarifa modositas a gyartmany-teljesitmeny alapjan"
	agenda-group "calculate"
	when
		$c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$pm: ParamDef( key == "BASETARIFF-TEMP", value == "UNKNOWN" )
		$v : Vehicle( typeCode == $c1.value )
		$m : MakeDef( make == $v.make )
		$p : PowerModFactorDef( makeCode == $m.makeCode )
		eval ( isBetween( $p.getPowerMin(), 
						  $p.getPowerMax(), 
						  $v.getMaximumNettoPower() ) )
		eval ( $m.isValid( $q.getStartDate() ) )
		eval ( $p.isValid( $q.getStartDate() ) ) 
				
	then
		int baseTariff = $q.getResult().getBaseTariff();
		baseTariff = (int)Math.round(baseTariff * $p.getValue() );
		modify ( $pm ) { setValue( Integer.toString( baseTariff ) ); }		
		//insert(new ModFactor("MAKE-POWER", 1.0, $v, "MULTIPLIER"));
		insert(new ModFactor("MAKE-POWER", $p.getValue(), $v, "MULTIPLIER"));
end

rule "hengerurtartalom szorzo"
	agenda-group "calculate"
	when
		$c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == $c1.value )
		$c : CubicCapacityModFactorDef( )
		eval ( isBetween( $c.getCapacityMin(), 
						  $c.getCapacityMax(), 
						  $v.getCubicCapacity() ) )
		eval ( $c.isValid( $q.getStartDate() ) ) 
				
	then		
		insert(new ModFactor("CUBIC-CAPACITY", $c.getValue(), $v, "MULTIPLIER"));
end

rule "teruleti besorolas-korcsoport szorzo (termeszetes szemely)"
	agenda-group "calculate"
	when
	    $c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == $c1.value )		
		$p : Partner( genderCode != Constants.JOGI_SZEMELY )
		$a : AreaDef( postCode == $p.postCode )
		$mf: PartnerAgeModFactorDef( areaCode == $a.areaCode )
		eval ( isBetween( $mf.getAgeMin(), 
						  $mf.getAgeMax(), 
						  yearsBetween($p.getDateOfBirth(), $q.getStartDate() ) ) )
		eval ( $a.isValid( $q.getStartDate() ) )
		eval ( $mf.isValid( $q.getStartDate() ) )
				 	   				
	then
		insert(new ModFactor("AREA", $mf.getValue(), $p, "MULTIPLIER"));
end

rule "teruleti besorolas szorzo (jogi szemely)"
	agenda-group "calculate"
	when
	    $c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == $c1.value )
		$p : Partner( genderCode == Constants.JOGI_SZEMELY )
		$a : AreaDef( postCode == $p.postCode )
		$mf: LegalModFactorDef( areaCode == $a.areaCode )
		eval ( $a.isValid( $q.getStartDate() ) &&
		 	   $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("AREA", $mf.getValue(), $p, "MULTIPLIER"));
end

rule "jarmu eletkor szorzo"
	agenda-group "calculate"
	when
	    $c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == $c1.value && eval( !isNullInt( yearOfProduction ) ) )
		$mf: VehicleAgeModFactorDef()
		eval ( isBetween( $mf.getAgeMin(),
						  $mf.getAgeMax(),
						  ( getYear( $q.getStartDate() ) - $v.getYearOfProduction() ) ) )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("VEHICLE-AGE", $mf.getValue(), $v, "MULTIPLIER"));
end

rule "vezetoi engedely szorzo"
	agenda-group "calculate"
	when
	    $c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == $c1.value )
		//Azért kell a Partnerhoz betenni ezt a feltételt, mert ha nincs itt, és a yearOfDrivingLicense = null,
		//akkor az 1. eval elszáll a new DateTime miatt.
		$p : Partner( genderCode != 3 && eval( !isNullInt( yearOfDrivingLicense ) ) )
		$mf: DrivingLicenseModFactorDef()
		eval ( isBetween( $mf.getSinceMin(),
						  $mf.getSinceMax(),
						  yearsBetween(new DateTime($p.getYearOfDrivingLicense(),1,1,0,0,0,0).toDate(), $q.getStartDate() ) ) )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("DRIVING-LICENSE", $mf.getValue(), $p, "MULTIPLIER"));
end

rule "vezetoi engedely szorzo (termeszetes szemelynel ha nincs megadva, akkor 1.03)"
	agenda-group "calculate"
	when
	    $c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == $c1.value )
		$p : Partner( genderCode != 3 && eval( isNullInt( yearOfDrivingLicense ) ) )
		$mf: DrivingLicenseModFactorDef()
		eval ( isBetween( $mf.getSinceMin(), $mf.getSinceMax(), 0 ) )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		$p.setYearOfDrivingLicense(null);
		insert(new ModFactor("DRIVING-LICENSE", $mf.getValue(), $p, "MULTIPLIER"));
end

rule "dijfizetes gyakorisaga szorzo"
	agenda-group "calculate"
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$mf: PaymentFreqModFactorDef( frequencyCode == $q.paymentFrequency )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("PAYMENT-FREQUENCY", $mf.getValue(), $q, "MULTIPLIER"));
end

rule "Bonus-Malus szorzo"
	agenda-group "calculate"
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$bm: BonusMalusModFactorDef( bonusMalus == $q.bonusMalus )
		$v : Vehicle()
		$vt: VehicleTypeDef( typeCode == $v.typeCode, bonusMalus == true )	
		eval ( $bm.isValid( $q.getStartDate() ) )
		eval ( $vt.isValid( $q.getStartDate() ) )		
				
	then
		insert(new ModFactor("BONUS-MALUS", $bm.getValue(), $q, "MULTIPLIER") );
end

/*****************************************************************************
				 Kedvezmények, pótdíjak számolása 
*****************************************************************************/
rule "uzemeltetesi jelleg potdij"
	agenda-group "calculate"
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v: Vehicle( eval (!Constants.OM_NORMAL.equals(operationalModality) ) )
		$mf: ModFactorDef( modfactorType == Constants.MF_UZEMELTETES )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("OPERATIONAL-MODALITY", $mf.getValue(), $v, "SURCHARGE"));
end

rule "mkbb casco egyuttkotes kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( takeOutWithMKBBCasco == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_MKBB_CASCO )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("MKBB-CASCO", $mf.getValue(), $q, "DISCOUNT"));
end

rule "nem mkbb casco egyuttkotes kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( nonMKBBCasco == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_NEM_MKBB_CASCO )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("NON-MKBB-CASCO", $mf.getValue(), $q, "DISCOUNT"));
end

rule "fizetesi mod kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( paymentMethodSurcharge == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_BANKI_CSOPORTOS_LEHIVAS_VAGY_ATUTALAS )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("PAYMENT-METHOD", $mf.getValue(), $q, "DISCOUNT"));
end


rule "ertekesitesi partner kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( marketingPartner == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_ERTEKESITESI_PARTNER )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("MARKETING-PARTNER", $mf.getValue(), $q, "DISCOUNT"));
end

rule "gyermek kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( childPreference == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_GYERMEK )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("CHILD", $mf.getValue(), $q, "DISCOUNT"));
end

rule "mkbb partner kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( mkbPartner == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_MKB_PARTNER )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("MKB-PARTNER", $mf.getValue(), $q, "DISCOUNT"));
end

rule "email kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( emailGranted == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_EMAIL )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("EMAIL", $mf.getValue(), $q, "DISCOUNT"));
end

rule "extra karmentessegi kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( extraClaimFree == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_EXTRA_KARMENTESSEG )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("EXTRA-CLAIM-FREE", $mf.getValue(), $q, "DISCOUNT"));
end

rule "nemet-magyar keresk. kamara tag kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( )
		$p : Partner( gerHunCCMember == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_NEMET_MAGYAR_KERESK_KAMARA_TAGSAG )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("GER-HUN-CC-MEMBER", $mf.getValue(), $p, "DISCOUNT"));
end

rule "kisvallalkozoi kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( )
		$p : Partner( entrepreneur == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_KISVALLALKOZO )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("ENTREPRENEUR", $mf.getValue(), $p, "DISCOUNT"));
end

rule "kampany kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( campaign == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_KAMPANY )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("CAMPAIGN", $mf.getValue(), $q, "DISCOUNT"));
end

rule "kozalkalmazotti kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( )
		$p : Partner( publicEmployee == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_KOZALKALMAZOTT )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("PUBLIC-EMPLOYEE", $mf.getValue(), $p, "DISCOUNT"));
end

rule "koztisztviseloi kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( )
		$p : Partner( governmentEmployee == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_KOZTISZTVISELO )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("GOVERNMENT-EMPLOYEE", $mf.getValue(), $p, "DISCOUNT"));
end

rule "tanar kedvezmeny"
	agenda-group "calculate"
	when
		$q : Quote( )
		$p : Partner( teacher == true )
		$mf: ModFactorDef( modfactorType == Constants.MF_TANAR )
		eval ( $mf.isValid( $q.getStartDate() ) )
				
	then
		insert(new ModFactor("TEACHER", $mf.getValue(), $p, "DISCOUNT"));
end


/*****************************************************************************
							  Tarifálás 
*****************************************************************************/
rule "quote szorzok"
	agenda-group "calculate"
	salience -30
	
    when
        $q : Quote()
        $modfactors : ArrayList()
                   		from collect( ModFactor( quote == $q ) )

    then
    	for (Object mf : $modfactors){
    		$q.addModFactor((ModFactor) mf);
    	}
end

rule "jarmu szorzok"
	agenda-group "calculate"
	salience -30
	
    when
        $q : Quote()
        $v : Vehicle( quote == $q )
        $modfactors : ArrayList()
                   		from collect( ModFactor( vehicle == $v ) )

    then
    	for (Object mf : $modfactors){
    		$v.addModFactor((ModFactor) mf);
    	}
end

rule "partner szorzok"
	agenda-group "calculate"
	salience -30
	
    when
        $q : Quote()
        $p : Partner( quote == $q )
        $modfactors : ArrayList()
                   		from collect( ModFactor( partner == $p ) )

    then
    	for (Object mf : $modfactors){
    		$p.addModFactor((ModFactor) mf);
    	}
end

rule "hatarozatlan ideju kgfb eves premium szamolasa"
	agenda-group "calculate"
	salience -50
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle()
		$p : Partner()
		$pf: PaymentFreqDef( frequencyCode == $q.paymentFrequency)
		$param : ParamDef( key == "DOUBLE_PRECISION" )
		$bt : ParamDef( key == "BASETARIFF-TEMP" )
        $quotemf : Double()
	        			from accumulate ( $mf : ModFactor( quote == $q, $val : value, discount == false, surcharge == false ),
	                          init(double m = 1.0;),
	                          action(
	                          	m *= $val;
	                          ),
	                          result(m) 
	                        )
        $partnermf : Double()
	        			from accumulate ( $mf : ModFactor( partner == $p, $val : value, discount == false, surcharge == false ),
	                          init(double m = 1.0;),
	                          action(
	                          	m *= $val;
	                          ),
	                          result(m) 
	                        )
        $vehiclemf : Double()
	        			from accumulate ( $mf : ModFactor( code != "MAKE-POWER", vehicle == $v, $val : value, discount == false, surcharge == false ),
	                          init(double m = 1.0;),
	                          action(
	                          	m *= $val;
	                          ),
	                          result(m) 
	                        )    
		$discounts : Double()
	        			from accumulate ( $mf : ModFactor( $val : value, discount == true ),
	                       	init(double m = 1.0;),
	                       	action(
	                       		m *= $val;
	                       	),
	                       	result(m) 
	                   )    
	   	$surcharges : Double()
	        			from accumulate ( $mf : ModFactor( $val : value, surcharge == true ),
	                       	init(double m = 1.0;),
	                       	action(
	                       		m *= $val;
	                       	),
	                       	result(m) 
	                   )     	 	                              	                                  	                                           					
	then
		int baseTariff = $q.getResult().getBaseTariff();
		
		if (!"UNKNOWN".equals($bt.getValue() ) ) {
			$q.getResult().setBaseTariff( Integer.parseInt( $bt.getValue() ) );
			baseTariff = Integer.parseInt( $bt.getValue() );
		}
		
		int precision = Integer.parseInt( $param.getValue() );
		
		if ( $discounts < 0.5 ) {			
			insert( Message.create("26", String.valueOf( round( (1 - $discounts) * 100, precision ) ) ) );
			$discounts = 0.5;
		}

		double mult = round($quotemf * $partnermf * $vehiclemf * $discounts * $surcharges, precision);
		int premium = (int)Math.round(baseTariff * mult);
		int installmentPremium = (int)Math.round(premium / (double)$pf.getInstallmentDivisor() );
//		System.out.println("qoute: " + $quotemf); 
//		System.out.println("vehicle: " + $vehiclemf); 
//		System.out.println("partner: " + $partnermf);
//		System.out.println("discounts: " + $discounts);
//		System.out.println("surcharges: " + $surcharges); 
//		System.out.println("mult: " + mult); 
//		System.out.println(baseTariff * mult); 
//		System.out.println("premium: " + premium); 
		
		$q.getResult().setYearlyPremium( premium );
		$q.getResult().setInstallmentPremium( installmentPremium );
		
		$q.getResult().setOtherMultiplier( round($quotemf * $partnermf * $vehiclemf, precision) );
		$q.getResult().setDiscountMultiplier( round($discounts, precision) );
		$q.getResult().setSurchargeMultiplier( round($surcharges, precision) );
		$q.getResult().setTotalMultiplier( mult );
		
end

rule "hatarozott ideju kgfb teljes premium szamolasa"
	agenda-group "calculate"
	salience -50
	when
		$q : Quote( durationType == Constants.DT_HATAROZOTT )
		$v : Vehicle()
		$p : Partner()
	then
		int baseTariff = $q.getResult().getBaseTariff();
		
		//int numOfDays = daysBetween( $q.getStartDate(), $q.getEndDate() );
		//double dailyPremium = baseTariff / 30.0;
		
		//int total = (int)Math.round(dailyPremium * numOfDays);
		//$q.getResult().setTotalPremium( total );
		
		$q.getResult().setTotalPremium( baseTariff );
		
		$q.getResult().setOtherMultiplier( 1.0 );
		$q.getResult().setDiscountMultiplier( 1.0 );
		$q.getResult().setSurchargeMultiplier( 1.0 );
		$q.getResult().setTotalMultiplier( 1.0 );
		
		//Beszúrunk egy info messaget : ("A teljes díj 30 napra szól. Határozott időre szóló KGFB csak 30 napra köthető!")
		insert( Message.create("35") );
	
end


