package MTPLTariffRules

import hu.dbx.screek.model.facts.*;
import hu.dbx.screek.model.*;
import org.joda.time.DateTime;
import java.util.Date;

function boolean isNullString(String s) {
		return ("".equals(s) || s == null);
}

function String getFormattedDate(Date d) {
		DateTime d1 = new DateTime(d);
		String date = String.valueOf(d1.getYear());
		if (d1.getMonthOfYear() < 10) {
			date = date + "-0" + String.valueOf(d1.getMonthOfYear() );
		}
		else {
			date = date + "-" + String.valueOf(d1.getMonthOfYear() );
		}
		if (d1.getDayOfMonth() < 10) {
			date = date + "-0" + String.valueOf(d1.getDayOfMonth() );
		}
		else {
			date = date + "-" + String.valueOf(d1.getDayOfMonth() );
		}
				
		return date;
}

function boolean isNullInt(Integer i) {
		return (i == null || i == 0);
}

function boolean firstDateIsBiggerOrEqualsThanSecondDate(DateTime q, DateTime v) {
	return ( q.isAfter(v) || q.isEqual(v) );
}

function boolean isBetween(Integer lowerBound, Integer upperBound, Integer value) {
	if ( value == null ) {
		return false;
	} 
	else {
		return (lowerBound <= value && upperBound >= value);
	}
}

function int yearsBetween(Date s, Date e) {
	return Math.abs(new DateTime(s).getYear() - new DateTime(e).getYear());  
}

/*****************************************************************************
				 Biztosítás szintű validációk 
*****************************************************************************/
rule "validate bonusMalus (isNull)"
    agenda-group "validate"
    lock-on-active
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN,
					eval( isNullString(bonusMalus) ) )
		$v : Vehicle()
		$vt: VehicleTypeDef( typeCode == $v.typeCode, bonusMalus == true )
	then
		$q.setBonusMalus(null);
		insert( Message.create("29") );
end

rule "validate bonusMalus (not valid)"
    agenda-group "validate"
    lock-on-active
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN,
					eval( !isNullString(bonusMalus) ) )
		not(exists BonusMalusModFactorDef( bonusMalus == $q.bonusMalus ) )	
	then
		insert( Message.create("30", $q.getBonusMalus() ) );
end

rule "validate startDate (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( startDate == null )
				
	then
		insert( Message.create("24") );		
end

rule "validate insurance duration (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( eval( isNullInt(durationType) ) )
		
	then
		$q.setDurationType(null);
		insert( Message.create("12") );		
end

rule "validate insurance duration (not valid)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( eval( !isNullInt(durationType) ) )
		not(exists DurationTypeDef( durationType == $q.durationType ) )
		
	then
		insert( Message.create("11", $q.getDurationType().toString() ) );		
end

rule "validate payment method (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN &&  
					eval( isNullInt(paymentMethod) ) )
	then
		$q.setPaymentMethod(null);
		insert( Message.create("6") );		
end

rule "validate payment method (not valid)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN &&  
					eval( !isNullInt(paymentMethod) ) )
		not(exists PaymentMethodDef( paymentMethod == $q.paymentMethod ) )
			
	then
		insert( Message.create("5", $q.getPaymentMethod().toString() ) );		
end

rule "validate payment frequency (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN && 
					eval( isNullInt(paymentFrequency) ) )
	then
		$q.setPaymentFrequency(null);
		insert( Message.create("8") );		
end

rule "validate payment frequency (not valid)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN &&
					eval( !isNullInt(paymentFrequency) ) )
		not(exists PaymentFreqDef( frequencyCode == $q.paymentFrequency ) )
			
	then
		insert( Message.create("7", $q.getPaymentFrequency().toString() ) );
end

rule "validate payment frequency (not valid - havi eseten nem csoportos)"
    agenda-group "validate"
    lock-on-active    
	when
		$p : PaymentMethodDef( paymentMethod == Constants.PM_BANKI_CSOPORTOS_BESZEDES )
		$q : Quote( durationType == Constants.DT_HATAROZATLAN && 
					paymentMethod != $p.paymentMethod &&
					paymentFrequency == Constants.PF_HAVI )				
			
	then
		insert( Message.create("27") );
end

rule "validate payment frequency (not valid - bizonyos jármuveknel egy osszegben kell fizetni)"
    agenda-group "validate"
    lock-on-active     
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN, 
					paymentFrequency != Constants.PF_EVES )
		$v : Vehicle( typeCode == Constants.TC_KONNYU_POTKOCSI ||
					  typeCode == Constants.TC_NEHEZ_POTKOCSI ||
					  typeCode == Constants.TC_SZEMELYGEPKOCSI_UTANFUTO ||
					  typeCode == Constants.TC_LAKOKOCSI ||
					  typeCode == Constants.TC_MOTORKEREKPAR_UTANFUTO ||
					  typeCode == Constants.TC_SEGEDMOTOROS_KEREKPAR )
		$vt: VehicleTypeDef( typeCode == $v.typeCode )
	then
		insert( Message.create("28", $v.getTypeCode().toString(), $vt.getDescription() ) );
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (egyeb jarmu)"
	agenda-group "validate"
	lock-on-active
	salience -5 
	when
		$q : Quote()
		$v : Vehicle()
		$b : BaseTariffDef( durationType == $q.durationType,
		    		   vehicleType == $v.typeCode )
		$vt: VehicleTypeDef( typeCode == $v.typeCode )		    		   
		eval ( !$b.isValid( $q.getStartDate() ) )
		eval ( ($q.getDurationType() == Constants.DT_HATAROZATLAN && 
			   	$v.getTypeCode() != Constants.TC_TEHERGEPJARMU && 
		   	   	$v.getTypeCode() != Constants.TC_MOTORKEREKPAR && 
		   	   	$v.getTypeCode() != Constants.TC_AUTOBUSZ) ||
		   	   	($q.getDurationType() == Constants.DT_HATAROZOTT) ) 
	    not (exists (Message( code == "24") ) )	
	then	
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
		
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (motorkerekpar)"
	agenda-group "validate"
	lock-on-active
	salience -5 
	when
		$q : Quote()
		$v : Vehicle( typeCode == Constants.TC_MOTORKEREKPAR )
		$vt: VehicleTypeDef( typeCode == $v.typeCode )	
		$b : MotorBikeBaseTariffDef( durationType == $q.durationType ) 
		eval ( isBetween($b.getCubicCapacityMin(), $b.getCubicCapacityMax(), $v.getCubicCapacity() ) )
		eval ( !$b.isValid( $q.getStartDate() ) )
		not (exists (Message( code == "24") ) )
						
	then
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (autobusz)"
	agenda-group "validate"
	lock-on-active
	salience -5 
	when
		$q : Quote()
		$v : Vehicle( typeCode == Constants.TC_AUTOBUSZ )
		$vt: VehicleTypeDef( typeCode == $v.typeCode )			
		$b : AutoBusBaseTariffDef( durationType == $q.durationType )
		eval ( isBetween($b.getSeatingCapacityMin(), $b.getSeatingCapacityMax(), $v.getSeatingCapacity() ) )
		eval ( !$b.isValid( $q.getStartDate() ) )
		not (exists (Message( code == "24") ) )
						
	then
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
end

rule "Nincs ervenyes alaptarifa a megadott kezdodatumhoz (tehergepjarmu)"
	agenda-group "validate"
	lock-on-active
	salience -5 
	when
		$q : Quote()
		$p : Partner()
		$v : Vehicle( typeCode == Constants.TC_TEHERGEPJARMU )
		$vt: VehicleTypeDef( typeCode == $v.typeCode )
		$b : TruckBaseTariffDef( durationType == $q.durationType,
								 genderCode == $p.genderCode )
		eval ( isBetween( $b.getCarryingCapacityMin(), 
						  $b.getCarryingCapacityMax(), 
						  $v.getCarryingCapacity() ) )
		eval ( ( $p.getGenderCode() != Constants.JOGI_SZEMELY &&
				 isBetween( $b.getPartnerAgeMin(), 
				 			$b.getPartnerAgeMax(), 
				 			yearsBetween( $p.getDateOfBirth(), $q.getStartDate() ) ) ) || 
			   ( $p.getGenderCode() == Constants.JOGI_SZEMELY ) )
		eval ( !$b.isValid( $q.getStartDate() ) )
		not (exists (Message( code == "24") ) )				
	then
		insert( Message.create("33", getFormattedDate($q.getStartDate() ), $v.getTypeCode().toString(), $vt.getDescription() ) );
end

/*****************************************************************************
				        Jármű szintű validációk 
*****************************************************************************/
rule "validate dateOfFirstPutIntoTraffic (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU &&
					  dateOfFirstPutIntoTraffic == null )
				
	then
		insert( Message.create("25") );		
end

rule "validate vehicle typecode (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$v : Vehicle( eval( isNullInt(typeCode) ) )
		
	then
		$v.setTypeCode(null);
		insert( Message.create("4") );		
end

rule "validate vehicle typecode (not valid)"
    agenda-group "validate"
    lock-on-active    
	when
		$v : Vehicle( eval( !isNullInt(typeCode) ) )
		not(exists VehicleTypeDef( typeCode == $v.typeCode ) )
		
	then
		insert( Message.create("3", $v.getTypeCode().toString() ) );		
end

rule "validate vehicle typecode (not valid - proba rendszam nincs hatarozatlan esetben)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_PROBA_RENDSZAM )
		$vt: VehicleTypeDef( typeCode == $v.typeCode )
	then
		insert( Message.create("31", $v.getTypeCode().toString(), $vt.getDescription() ) );		
end

rule "validate vehicle typecode (not valid - segedmotoros kerekpar, trolibusz nincs hatarozott esetben)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZOTT )
		$v : Vehicle( typeCode == Constants.TC_SEGEDMOTOROS_KEREKPAR ||
					  typeCode == Constants.TC_TROLIBUSZ )
		$vt: VehicleTypeDef( typeCode == $v.typeCode )
	then
		insert( Message.create("32", $v.getTypeCode().toString(), $vt.getDescription() ) );		
end

rule "validate vehicle make (not valid)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU && 
					  eval ( !isNullString(make) ) )
		not(exists MakeDef( make == $v.make ) )
		
	then
		insert( Message.create("9", $v.getMake() ) );		
end

rule "validate vehicle make (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU && 
					  eval( isNullString(make) ))
		
	then
		insert( Message.create("10") );		
end

rule "validate operationalModality (not valid)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( eval( !isNullInt(operationalModality) ) )
		not(exists OperationalModalityDef( operationalModality == $v.operationalModality ) )
			
	then
		insert( Message.create("16", $v.getOperationalModality().toString() ) );
end

rule "validate operationalModality (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( eval( isNullInt(operationalModality) ) )
			
	then
		$v.setOperationalModality(null);
		insert( Message.create("17") );
end

rule "validate power (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU && 
					  eval( isNullInt(maximumNettoPower) ) )
			
	then
		$v.setMaximumNettoPower(null);
		insert( Message.create("18") );
end

rule "validate cubicCapacity (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( (typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_MOTORKEREKPAR) &&  
					  eval( isNullInt(cubicCapacity) ) )
			
	then
		$v.setCubicCapacity(null);
		insert( Message.create("19") );
end

rule "validate seatingCapacity (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_AUTOBUSZ && 
					  eval( isNullInt(seatingCapacity) ) )
			
	then
		$v.setSeatingCapacity(null);
		insert( Message.create("20") );
end

rule "validate carryingCapacity (isNull)"
    agenda-group "validate"
    lock-on-active
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_TEHERGEPJARMU && 
					  eval( isNullInt(carryingCapacity) ) )
			
	then
		$v.setCarryingCapacity(null);
		insert( Message.create("21") );
end

/*****************************************************************************
				        Partner szintű validációk 
*****************************************************************************/
rule "validate dateOfBirth (isNull)"
    agenda-group "validate"
    lock-on-active    
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_TEHERGEPJARMU )
		$p : Partner( dateOfBirth == null &&
					  genderCode != 3 )
				
	then
		insert( Message.create("23") );		
end

rule "validate postcode (isNull)"
    agenda-group "validate"    
    lock-on-active
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU )
		$p : Partner( eval( isNullInt(postCode) ) )
				
	then
		$p.setPostCode(null);
		insert( Message.create("2") );		
end

rule "validate postcode (not valid)"
    agenda-group "validate"  
    lock-on-active      
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU )
		$p : Partner( eval( !isNullInt(postCode) ) )
		not(exists AreaDef( postCode == $p.postCode ) )
		
	then
		insert( Message.create("1", $p.getPostCode().toString() ) );		
end

rule "validate genderCode (isNull)"
    agenda-group "validate"
    lock-on-active   
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_TEHERGEPJARMU )
		$p : Partner( eval( isNullInt( genderCode ) ) )
	then
		insert( Message.create("14") );		
end

rule "validate genderCode (not valid)"
    agenda-group "validate"
    lock-on-active        
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU || typeCode == Constants.TC_TEHERGEPJARMU )
		$p : Partner( eval( !isNullInt( genderCode ) ) )
		not(exists GenderDef( genderCode == $p.genderCode ) )
			
	then
		insert( Message.create("13", $p.getGenderCode().toString() ) );
end

rule "validate yearOfDrivingLicense (isNull)"
    agenda-group "validate"
    lock-on-active
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == Constants.TC_SZEMELYGEPJARMU )
		$p : Partner( eval( isNullInt(yearOfDrivingLicense) ) )
			
	then
		$p.setYearOfDrivingLicense(null);
		insert( Message.create("22") );
end



/*****************************************************************************
				        		Egyéb
*****************************************************************************/
rule "setting groupedCollection flag"
    agenda-group "validate"
    salience 50
    lock-on-active
	when
		$p : PaymentMethodDef( paymentMethod == Constants.PM_BANKI_CSOPORTOS_BESZEDES )
		$q : Quote( paymentMethod == $p.paymentMethod )
		
	then
		modify( $q ) { setGroupedCollection( true ) };		
end

rule "validate szemelygepjarmu discounts (startDate >= 2010 jan 1 and insuranceDuration = HATAROZATLAN)"
    agenda-group "validate"
    salience -50
    no-loop
	when
		$q : Quote( takeOutWithCasco == true ||
					childPreference == true ||
					mkbPartner == true ||
					online == true ||
					emailGranted == true ||
					extraDamageExemption == true ||
					groupedCollection == true )
		$v : Vehicle()
		$d : DurationTypeDef( durationType == Constants.DT_HATAROZATLAN )
		$vt: VehicleTypeDef( typeCode == Constants.TC_SZEMELYGEPJARMU )
		not (exists ( Message() ) )  
		eval ( !firstDateIsBiggerOrEqualsThanSecondDate(new DateTime($q.getStartDate()), new DateTime(2010, 1, 1, 0, 0, 0, 0) ) 
			  || ($d.getDurationType() != $q.getDurationType() ) 
			  || ($vt.getTypeCode() != $v.getTypeCode() ) )
		
	then
		insert( Message.create("15", "2009-12-31" ) );
		modify ( $q ) {	
			setTakeOutWithCasco(false),
			setChildPreference(false),
			setMkbPartner(false),
			setOnline(false),
			setEmailGranted(false),
			setExtraDamageExemption(false),
			setGroupedCollection(false)	
		}
end