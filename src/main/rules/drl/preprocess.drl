package MTPLTariffRules

import hu.dbx.screek.model.facts.*;
import hu.dbx.screek.model.*;
import org.joda.time.DateTime;
import java.util.Date;

function boolean isEqualsDate(DateTime s0, DateTime e0) {
	if (s0 == null || e0 == null) {
		return false;
	}
		
	DateTime temp = s0.withDate(s0.getYear(), s0.getMonthOfYear(), s0.getDayOfMonth());
	
	return temp.isEqual(e0);    
}

rule "setting groupedCollection flag (2011)"
    agenda-group "preprocess"
    lock-on-active
	when
		$p : PaymentMethodDef( paymentMethod == Constants.PM_BANKI_CSOPORTOS_BESZEDES || 
							   paymentMethod == Constants.PM_ATUTALAS )
		$q : Quote( paymentMethod == $p.paymentMethod )
		
	then
		modify( $q ) { setGroupedCollection( true ) };		
end

rule "setting campaign flag (if start = 2011.01.01)"
    agenda-group "preprocess"
    lock-on-active
	when
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		eval ( isEqualsDate( new DateTime($q.getStartDate()), new DateTime(2011, 1, 1, 0, 0, 0, 0) ) )
	then
		modify( $q ) { setCampaign( true ) };		
end

rule "modify cubicCapacity (cubicCapacityNotDefined == true)"
    agenda-group "preprocess"
    no-loop 
	when
		$c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$q : Quote( durationType == Constants.DT_HATAROZATLAN )
		$v : Vehicle( typeCode == $c1.value && 
					  cubicCapacityNotDefined == true )				  
			
	then
		modify( $v ) {
			setCubicCapacity(100) //850 ccm alatti értéket kell kapnia. 100 < 850 :)		
		}
end

rule "insert vehicle-type-def constants"
    agenda-group "preprocess"
	when
		$vt : VehicleTypeDef()
		not ( exists( Constant( name == $vt.constantName ) ) )
	then
		insert( new Constant($vt.getConstantName(), $vt.getTypeCode() ) );		
end
